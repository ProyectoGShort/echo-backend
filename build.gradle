allprojects {
	group = "xyz.proyectogshort.${rootProject.name}"

	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	repositories {
		mavenCentral()
	}

	java {
		sourceCompatibility = '20'
	}

	dependencies {
		compileOnly 'org.projectlombok:lombok:1.18.28'
		annotationProcessor 'org.projectlombok:lombok:1.18.28'
	}
}

subprojects {
	sourceSets {
		main {
			java { srcDirs = ['main'] }
			resources { srcDirs = ['main/resources'] }
		}
	}

	 bootJar {
		enabled = false
	 }

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

		if (project.name != "shared") {
			implementation project(":shared")

			implementation 'org.liquibase:liquibase-core:4.23.1'
			runtimeOnly 'org.postgresql:postgresql'
		}
	}
}

buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:3.1.2'
	}
}

sourceSets {
	main {
		java { srcDirs = ['apps/main'] }
		resources { srcDirs = ['apps/main/resources'] }
	}
}

dependencies {
	implementation project(":echo")
	implementation project(":test")
	implementation project(":shared")

	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
}
